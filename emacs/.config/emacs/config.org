#+TITLE: Emacs configuration
#+AUTHOR: eduardomcv
#+OPTIONS: toc:2

* Table of contents :toc:
- [[#package-manager][Package manager]]
- [[#general-emacs-settings][General emacs settings]]
- [[#start-server][Start server]]
  - [[#increase-size-of-sub-process-chunks][Increase size of sub-process chunks]]
  - [[#garbage-collector][Garbage collector]]
  - [[#indentation][Indentation]]
  - [[#backup-handling][Backup handling]]
  - [[#update-buffers-automatically][Update buffers automatically]]
  - [[#remember-last-place-visited-in-files][Remember last place visited in files]]
  - [[#remember-recent-files][Remember recent files]]
  - [[#auto-pairs][Auto-pairs]]
  - [[#use-no-littering-to-tidy-up-emacs-configuration-files][Use no-littering to tidy up emacs configuration files]]
  - [[#dired][Dired]]
  - [[#eldoc][Eldoc]]
  - [[#eww][Eww]]
- [[#evil-mode][Evil mode]]
- [[#ui][UI]]
  - [[#theme][Theme]]
  - [[#fonts][Fonts]]
  - [[#frames][Frames]]
  - [[#smooth-scrolling][Smooth scrolling]]
  - [[#remove-menu-toolbar-and-scroll-bar][Remove menu, toolbar and scroll bar]]
  - [[#enable-visual-lines-word-wrap][Enable visual lines (word wrap)]]
  - [[#highlight-current-line][Highlight current line]]
  - [[#line-numbers-and-column][Line numbers and column]]
  - [[#enable-which-key][Enable which key]]
  - [[#vertical-completion-ui-vertico][Vertical completion UI (vertico)]]
  - [[#rich-annotations][Rich annotations]]
  - [[#dashboard][Dashboard]]
  - [[#highlight-todo-comments][Highlight TODO comments]]
  - [[#indentation-guides][Indentation guides]]
  - [[#render-whitespace][Render whitespace]]
- [[#completion-corfu][Completion (corfu)]]
- [[#languages][Languages]]
  - [[#lsp-configuration][LSP configuration]]
  - [[#markdown-mode][Markdown mode]]
  - [[#python-venv-support][Python venv support]]
  - [[#snippets][Snippets]]
- [[#treesitter][Treesitter]]
- [[#git][Git]]
  - [[#magit][Magit]]
  - [[#git-signs][Git signs]]
- [[#undo-tree][Undo tree]]
- [[#fuzzy-finder][Fuzzy finder]]
- [[#project-detection][Project detection]]
- [[#formatting][Formatting]]
- [[#linting][Linting]]
- [[#org-mode][Org mode]]
  - [[#org-configuration][Org configuration]]
  - [[#table-of-contents][Table of contents]]
  - [[#org-bullets][Org bullets]]
- [[#terminal][Terminal]]
  - [[#vterm][Vterm]]
  - [[#vterm-toggle][Vterm toggle]]
- [[#keybinds][Keybinds]]

* Package manager
#+begin_src emacs-lisp
(require 'package)
(add-to-list 'package-archives
             '("melpa" . "https://melpa.org/packages/"))
(package-initialize)

(require 'use-package)
(setq use-package-always-ensure t)
#+end_src

* General emacs settings

* Start server
#+begin_src emacs-lisp
(server-start)
#+end_src

** Increase size of sub-process chunks
#+begin_src emacs-lisp
(setq read-process-output-max (* 1024 1024))
#+end_src

** Garbage collector
#+begin_src emacs-lisp
;; Increase memory threshold
(setq gc-cons-threshold 100000000)

(use-package gcmh
  :ensure t
  :init
  (gcmh-mode 1)
  :config
  (setq gcmh-idle-delay 0.5
        gcmh-high-cons-threshold (* 100 1024 1024)))
#+end_src

** Indentation
#+begin_src emacs-lisp
;; Use spaces by default
(setq-default indent-tabs-mode nil)

;; Default tab width of 4
(setq-default tab-width 4)

;; Use width of 2 for elisp
(add-hook 'emacs-lisp-mode-hook
          (lambda ()
            (setq tab-width 2)))
#+end_src

** Backup handling
#+begin_src emacs-lisp
(setq version-control t)
(setq delete-old-versions t)
(setq kept-new-versions 6)
(setq kept-old-versions 2)
#+end_src

** Update buffers automatically
#+begin_src emacs-lisp
(global-auto-revert-mode 1)
(setq global-auto-revert-non-file-buffers t)
#+end_src

** Remember last place visited in files
#+begin_src emacs-lisp
(save-place-mode 1)
#+end_src

** Remember recent files
#+begin_src emacs-lisp
(recentf-mode 1)
(run-at-time nil 300 'recentf-save-list)
#+end_src

** Auto-pairs
#+begin_src emacs-lisp
(electric-pair-mode t)
#+end_src

** Use no-littering to tidy up emacs configuration files
#+begin_src emacs-lisp
(use-package no-littering
  :config
  ;; Store custom file in etc/
  (setq custom-file (no-littering-expand-etc-file-name "custom.el"))
  (when (file-exists-p custom-file)
    (load custom-file 'noerror))

  ;; Store backups file in var/
  (setq backup-directory-alist
        `(("." . ,(no-littering-expand-var-file-name "backups/"))))
  ;; Store auto-save files in var/
  (setq auto-save-file-name-transforms
        `((".*" ,(no-littering-expand-var-file-name "auto-save/") t))))
#+end_src

** Dired
#+begin_src emacs-lisp
;; Always be recursive
(setq dired-recursive-copies 'always)
(setq dired-recursive-deletes 'always)
;; Re-use existing dired windows
(setq dired-dwim-target t)
;; Auto-refresh dired buffers when files change on disk
(setq global-auto-revert-non-file-buffers t)

;; Add colors to dired
(use-package diredfl
  :ensure t
  :hook (dired-mode . diredfl-mode))
#+end_src

** Eldoc
#+begin_src emacs-lisp
;; Make eldoc use bottom buffer, and limit height to 20%
(add-to-list 'display-buffer-alist
             '("^\\*eldoc"
               (display-buffer-reuse-window display-buffer-at-bottom)
               (window-height . 0.20)
               (dedicated . t)))

;; allow eldoc to show multiple lines
(setq eldoc-echo-area-use-multiline-p t)

(defun custom/close-eldoc-window-on-next-command ()
  "Close eldoc window on the next command (unless switching windows or toggling)."
  (let ((win (get-buffer-window "*eldoc*")))
    (when win
      (unless (or
               (eq (selected-window) win)
               (eq this-command 'custom/toggle-eldoc-tooltip)
               (eq this-command 'other-window)
               (eq this-command 'evil-window-down)
               (eq this-command 'evil-window-up)
               (eq this-command 'evil-window-next)
               (eq this-command 'evil-window-prev)
               (eq this-command 'evil-window-mru))
        (delete-window win))))

  ;; Only remove the hook if the window is actually gone
  (unless (get-buffer-window "*eldoc*")
    (remove-hook 'pre-command-hook 'custom/close-eldoc-window-on-next-command)))

(defun custom/toggle-eldoc-tooltip ()
  "Toggle eldoc window. If visible, will auto-close on the next cursor movement."
  (interactive)
  (let ((win (get-buffer-window "*eldoc*")))
    (if win
        (progn
          (delete-window win)
          (remove-hook 'pre-command-hook 'custom/close-eldoc-window-on-next-command))
      (eldoc-doc-buffer t)
      (add-hook 'pre-command-hook 'custom/close-eldoc-window-on-next-command))))
#+end_src

** Eww
#+begin_src emacs-lisp
;; eww, i hate it
(setq browse-url-browser-function 'browse-url-default-browser)
#+end_src

* Evil mode
#+begin_src emacs-lisp
(use-package evil
  :init
  (setq evil-want-integration t)
  (setq evil-want-keybinding nil)
  (setq evil-want-C-u-scroll t)
  (setq evil-undo-system 'undo-tree)
  :config
  (evil-mode 1)
  (define-key evil-motion-state-map (kbd "SPC") nil))

(use-package evil-collection
  :after evil
  :config
  (evil-collection-init))

(use-package evil-commentary
  :after evil
  :config
  (evil-commentary-mode 1))

(use-package evil-surround
  :ensure t
  :config
  (global-evil-surround-mode 1))
#+end_src

* UI
** Theme
#+begin_src emacs-lisp
(use-package catppuccin-theme
  :config
  (setq catppuccin-flavor 'mocha)
  (load-theme 'catppuccin :no-confirm))
#+end_src

** Fonts
#+begin_src emacs-lisp
(set-face-attribute 'default nil
                    :font "Iosevka"
                    :height 160
                    :weight 'regular)

(set-face-attribute 'variable-pitch nil
                    :font "Iosevka Aile"
                    :height 160
                    :weight 'regular)

(set-face-attribute 'fixed-pitch nil
                    :font "Iosevka Term"
                    :height 160
                    :weight 'regular)

(set-face-attribute 'font-lock-comment-face nil :slant 'italic)
(set-face-attribute 'font-lock-keyword-face nil :slant 'italic)

(add-to-list 'default-frame-alist '(font . "Iosevka-16"))

(setq-default line-spacing 0.1)

(use-package nerd-icons)

;; Specific ligature setup for Iosevka
(use-package ligature
  :config
  (ligature-set-ligatures '('prog-mode 'text-mode) '("<---" "<--"  "<<-" "<-" "->" "-->" "--->" "<->" "<-->"
                                                     "<--->" "<---->" "<!--" "<==" "<===" "<=" "=>" "=>>"
                                                     "==>" "===>" ">=" "<=>" "<==>" "<===>" "<====>" "<!---"
                                                     "<~~" "<~" "~>" "~~>" "::" ":::" "==" "!=" "===" "!=="
                                                     ":=" ":-" ":+" "<*" "<*>" "*>" "<|" "<|>" "|>" "+:" "-:"
                                                     "=:" "<******>" "++" "+++"))
  (global-ligature-mode t))
#+end_src

** Frames
#+begin_src emacs-lisp
;; Default size for frames
(add-to-list 'default-frame-alist '(width . 120))
(add-to-list 'default-frame-alist '(height . 45))
;; Titlebar
(add-to-list 'default-frame-alist '(ns-transparent-titlebar . t))
(add-to-list 'default-frame-alist '(ns-appearance . dark))
;; Transparency
;; (set-frame-parameter nil 'alpha 97)
;; (add-to-list 'default-frame-alist '(alpha . 97))
#+end_src

** Smooth scrolling
#+begin_src emacs-lisp
(setq pixel-scroll-precision-large-scroll-height 40.0)
(setq pixel-scroll-precision-interpolation-factor 1.0)
(setq pixel-scroll-precision-interpolate-page t)
(pixel-scroll-precision-mode 1)
#+end_src

** Remove menu, toolbar and scroll bar
#+begin_src emacs-lisp
(menu-bar-mode -1)
(tool-bar-mode -1)
(scroll-bar-mode -1)
#+end_src

** Enable visual lines (word wrap)
#+begin_src emacs-lisp
(global-visual-line-mode 1)
#+end_src

** Highlight current line
#+begin_src emacs-lisp
(global-hl-line-mode 1)
#+end_src

** Line numbers and column
#+begin_src emacs-lisp
(setq-default display-line-numbers-type 'relative)   ; Relative numbers
(setq-default display-line-numbers-width 3)          ; Set line number column width to 3
(setq-default display-line-numbers-grow-only t)      ; Don't shrink the column
(global-display-line-numbers-mode 1)
#+end_src

** Enable which key
#+begin_src emacs-lisp
(which-key-mode 1)
#+end_src

** Vertical completion UI (vertico)
#+begin_src emacs-lisp
(use-package vertico
  :init
  (vertico-mode)
  :bind (:map vertico-map
              ("C-j" . vertico-next)
              ("C-k" . vertico-previous))
  :config
  (setq vertico-resize t)
  (setq vertico-count 15)
  :custom
  (vertico-cycle t))
#+end_src

** Rich annotations
#+begin_src emacs-lisp
(use-package marginalia
  :init
  (marginalia-mode))
#+end_src

** Dashboard
#+begin_src emacs-lisp
(use-package dashboard
  :ensure t
  :init
  (setq initial-buffer-choice (lambda () (get-buffer-create dashboard-buffer-name)))
  :config
  (set-face-attribute 'dashboard-text-banner nil :slant 'normal)
  (setq dashboard-startup-banner (expand-file-name "banner.txt" user-emacs-directory))

  (setq dashboard-center-content t)
  (setq dashboard-vertically-center-content t)

  (setq dashboard-display-icons-p t)
  (setq dashboard-icon-type 'nerd-icons)

  (setq dashboard-set-heading-icons t)
  (setq dashboard-set-file-icons t)

  (setq dashboard-items '((recents   . 5)
                          (bookmarks . 5)
                          (projects  . 5)
                          (agenda    . 5)
                          (registers . 5)))

  (setq dashboard-item-shortcuts '((recents   . "r")
                                   (bookmarks . "m")
                                   (projects  . "p")
                                   (agenda    . "a")
                                   (registers . "e")))

  (dashboard-setup-startup-hook))
#+end_src

** Highlight TODO comments
#+begin_src emacs-lisp
(use-package hl-todo
  :hook ((prog-mode . hl-todo-mode)
         (text-mode . hl-todo-mode))
  :config
  (setq hl-todo-highlight-punctuation ":")
  ;; Keywords
  (setq hl-todo-keyword-faces
        '(
          ("TODO"     . "#96CDFB")
          ("FIXME"       . "#F28FAD")
          ("HACK"       . "#FAE3B0")
          ("DEPRECATED"      . "#E8A2AF")
          ("NOTE"       . "#ABE9B3"))))
#+end_src

** Indentation guides
#+begin_src emacs-lisp
(use-package highlight-indent-guides
  :ensure t
  :hook ((prog-mode . highlight-indent-guides-mode))
  :config
  (setq highlight-indent-guides-method 'character)
  (setq highlight-indent-guides-responsive 'top)
  (setq highlight-indent-guides-auto-enabled nil)

  (set-face-foreground 'highlight-indent-guides-character-face "#45475a")
  (set-face-foreground 'highlight-indent-guides-top-character-face "#cba6f7")
  (set-face-foreground 'highlight-indent-guides-stack-character-face "#45475a"))
#+end_src

** Render whitespace
#+begin_src emacs-lisp
(use-package whitespace
  :ensure nil ;; built-in
  :hook (before-save . whitespace-cleanup)
  :config
  (setq whitespace-style '(face
                           tabs
                           spaces
                           trailing
                           space-before-tab
                           newline
                           empty
                           indentation)))
#+end_src

* Completion (corfu)
#+begin_src emacs-lisp
;; Fallback to completion when pressing TAB
(setq tab-always-indent 'complete)
;; Disable Ispell completion
(setq text-mode-ispell-word-completion nil)

(use-package corfu
  :ensure t
  :init
  (global-corfu-mode)
  :config
  (corfu-popupinfo-mode)

  (general-def
    :keymaps 'corfu-map
    "C-j" 'corfu-next
    "C-k" 'corfu-previous
    "TAB" 'corfu-next
    "S-TAB" 'corfu-previous
    "C-SPC" 'corfu-insert
    "M-h" 'corfu-popupinfo-toggle
    "M-j" 'corfu-popupinfo-scroll-up
    "M-k" 'corfu-popupinfo-scroll-down)
  :custom
  (corfu-cycle t)
  (corfu-auto t)
  (corfu-auto-delay 0.2)
  (corfu-auto-prefix 2)
  (corfu-quit-no-match 'separator)
  (corfu-popupinfo-delay '(0.1 . 1.0))
  :custom-face
  (corfu-default ((t (:background "#1e1e2e" :foreground "#cdd6f4"))))
  (corfu-border ((t (:inherit region :background unspecified))))
  (corfu-current ((t (:background "#313244" :foreground "#cdd6f4" :weight bold))))
  (corfu-bar ((t (:background "#585b70")))))

(use-package kind-icon
  :ensure t
  :after corfu
  :custom
  (kind-icon-blend-background t)
  (kind-icon-default-face 'corfu-default)
  :config
  (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter))

(use-package cape
  :bind ("C-c p" . cape-prefix-map)
  :init
  (add-hook 'completion-at-point-functions #'cape-dabbrev)
  (add-hook 'completion-at-point-functions #'cape-file)
  (add-hook 'completion-at-point-functions #'cape-elisp-block)
  (add-hook 'completion-at-point-functions #'cape-history))
#+end_src

* Languages

** LSP configuration
#+begin_src emacs-lisp
(use-package eglot
  :hook
  ((prog-mode . eglot-ensure))
  :config
  (advice-add 'eglot-completion-at-point :around #'cape-wrap-buster)

  ;; Add user bin to emacs PATH
  (add-to-list 'exec-path (expand-file-name "~/.local/bin"))
  (setenv "PATH" (concat (expand-file-name "~/.local/bin") ":" (getenv "PATH")))

  ;; Add mise shims to emacs PATH
  (let ((mise-path (expand-file-name "~/.local/share/mise/shims")))
    (when (file-directory-p mise-path)
      (add-to-list 'exec-path mise-path)
      (setenv "PATH" (concat mise-path ":" (getenv "PATH")))))

  ;; Add vtsls
  (add-to-list 'eglot-server-programs
               '((typescript-ts-mode tsx-ts-mode typescript-mode javascript-mode js-jsx-mode) . ("vtsls" "--stdio")))

        (setq-default eglot-workspace-configuration
              '(:basedpyright (:analysis (:autoSearchPaths t
                                          :useLibraryCodeForTypes t
                                          :diagnosticMode "openFilesOnly"))

                :vtsls (:enableMoveToFileCodeAction t
                        :autoUseWorkspaceTsdk t
                        :experimental (:completion (:enableServerSideFuzzyMatch t)))

                :typescript (:updateImportsOnFileMove (:enabled "always")
                             :suggest (:completeFunctionCalls t)
                             :preferences (:includePackageJsonAutoImports "on"
                                           :importModuleSpecifier "non-relative")
                             :inlayHints (:parameterNames (:enabled "all")
                                          :variableTypes (:enabled t)))

                :javascript (:updateImportsOnFileMove (:enabled "always")
                             :suggest (:completeFunctionCalls t)
                             :inlayHints (:parameterNames (:enabled "all")
                                          :variableTypes (:enabled t))))))
#+end_src

** Markdown mode
#+begin_src emacs-lisp
(use-package markdown-mode
  :ensure t
  :mode (("README\\.md\\'" . gfm-mode)
         ("\\.md\\'" . markdown-mode))
  :config
  (setq markdown-command "pandoc")
  (setq markdown-fontify-code-blocks-natively t)
  :bind (:map markdown-mode-map
              ("C-c C-e" . markdown-do)))
#+end_src

** Python venv support
#+begin_src emacs-lisp
(use-package pyvenv
  :ensure t
  :config
  (setq pyvenv-default-virtual-env-name ".venv"))
#+end_src

** Snippets
#+begin_src emacs-lisp
(use-package yasnippet
  :config
  (yas-global-mode 1))
#+end_src

* Treesitter
#+begin_src emacs-lisp
(setq-default treesit-font-lock-level 4)

(use-package treesit-auto
  :custom
  (treesit-auto-install 'prompt)
  :config
  (treesit-auto-add-to-auto-mode-alist 'all)
  (global-treesit-auto-mode))
#+end_src

* Git
** Magit
#+begin_src emacs-lisp
(use-package magit
  :commands magit-status
  :custom
  (magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1))
#+end_src

** Git signs
#+begin_src emacs-lisp
(use-package diff-hl
  :hook ((dired-mode . diff-hl-dired-mode)
         (magit-pre-refresh . diff-hl-magit-pre-refresh)
         (magit-post-refresh . diff-hl-magit-post-refresh))
  :init
  (global-diff-hl-mode)
  :config
  (diff-hl-flydiff-mode)
  (diff-hl-margin-mode)
  (setq diff-hl-margin-symbols-alist
        '((insert . "│")
          (delete . "│")
          (change . "│")
          (unknown . "│")
          (ignored . "│")))

  (custom-set-faces
   '(diff-hl-insert ((t (:foreground "#a6e3a1" :background nil))))
   '(diff-hl-delete ((t (:foreground "#f38ba8" :background nil))))
   '(diff-hl-change ((t (:foreground "#89b4fa" :background nil))))))
#+end_src

* Undo tree
#+begin_src emacs-lisp
(use-package undo-tree
  :init
  (global-undo-tree-mode)
  :config
  (setq undo-tree-auto-save-history t)
  ;; Store undo files in var/undo-tree-hist/
  (setq undo-tree-history-directory-alist
        `(("." . ,(no-littering-expand-var-file-name "undo-tree-hist/")))))
#+end_src

* Fuzzy finder
#+begin_src emacs-lisp
(use-package consult
  :init
  (setq xref-show-xrefs-function #'consult-xref)
  (setq xref-show-definitions-function #'consult-xref)
  :bind (
         ("C-c g" . consult-ripgrep)
         ("C-c f" . consult-fd)
         ("C-c b" . consult-buffer)
         ("C-c s" . consult-line)
         )
  :config
  (setq consult-async-min-input 0)
  ;; Show hidden files in fd
  (let ((fd-name (if (executable-find "fdfind") "fdfind" "fd")))
    (setq consult-fd-args (concat fd-name " --full-path --absolute-path --color=never --hidden --exclude .git"))))

(use-package orderless
  :custom
  (completion-styles '(orderless basic))
  (completion-category-overrides '((file (styles basic partial-completion)))))

(use-package consult-todo
  :config
  (defconst consult-todo--narrow
    '((?t . "TODO")
      (?f . "FIXME")
      (?h . "HACK")
      (?d . "DEPRECATED")
      (?n . "NOTE"))))
#+end_src

* Project detection
#+begin_src emacs-lisp
(use-package project
  :config
  (setq project-list-file (locate-user-emacs-file "projects"))
  (setq project-switch-commands 'project-find-file))
#+end_src

* Formatting
#+begin_src emacs-lisp
(use-package apheleia
  :config
  (setq apheleia-formatters-respect-indent-level nil)

  (setf (alist-get 'python-mode apheleia-mode-alist)
        '(ruff-isort ruff))
  (setf (alist-get 'python-ts-mode apheleia-mode-alist)
        '(ruff-isort ruff))

  (apheleia-global-mode +1))
#+end_src

* Linting
#+begin_src emacs-lisp
(use-package flymake
  :hook (prog-mode . flymake-mode)
  :config
  ;; Bitmaps for flymake (circles near the number column)
  (define-fringe-bitmap 'flymake-fringe-bitmap-circle
    (vector #b00000000
            #b00111100
            #b01111110
            #b01111110
            #b01111110
            #b01111110
            #b00111100
            #b00000000))

  (setq flymake-error-bitmap '(flymake-fringe-bitmap-circle compilation-error))
  (setq flymake-warning-bitmap '(flymake-fringe-bitmap-circle compilation-warning))
  (setq flymake-note-bitmap '(flymake-fringe-bitmap-circle compilation-info))

  (custom-set-faces
   '(flymake-error ((t (:underline (:style wave :color "#F28FAD")))))
   '(flymake-warning ((t (:underline (:style wave :color "#FAE3B0")))))
   '(flymake-note ((t (:underline (:style wave :color "#96CDFB")))))))

(use-package flymake-collection
  :hook (after-init . flymake-collection-hook-setup))
#+end_src

* Org mode
** Org configuration
#+begin_src emacs-lisp
;; Org directories
(setq org-directory "~/Repos/Org")
(setq org-agenda-files "~/Repos/Org/agenda.org")

;; Keep code blocks consistent
(setq org-edit-src-content-indentation 0)
(setq org-src-tab-acts-natively t)
(setq org-src-fontify-natively t)
(setq org-src-preserve-indentation t)
#+end_src

** Table of contents
#+begin_src emacs-lisp
(use-package toc-org
  :commands toc-org-enable
  :init
  (add-hook 'org-mode-hook 'toc-org-enable))
#+end_src

** Org bullets
#+begin_src emacs-lisp
(use-package org-bullets
  :config
  (add-hook 'org-mode-hook (lambda () (org-bullets-mode 1))))
#+end_src

* Terminal
** Vterm
#+begin_src emacs-lisp
(use-package vterm
  :ensure t
  :init (setq shell-file-name "/bin/zsh"))
#+end_src

** Vterm toggle
#+begin_src emacs-lisp
(use-package vterm-toggle
  :after vterm
  :config
  (setq vterm-toggle-fullscreen-p nil)
  (add-to-list 'display-buffer-alist
               '((lambda (buffer-or-name _)
                   (let ((buffer (get-buffer buffer-or-name)))
                     (with-current-buffer buffer
                       (or (equal major-mode 'vterm-mode)
                           (string-prefix-p vterm-buffer-name (buffer-name buffer))))))
                 (display-buffer-reuse-window display-buffer-in-side-window)
                 (side . bottom)
                 (dedicated . t)
                 (reusable-frames . visible)
                 (window-height . 0.3))))
#+end_src

* Keybinds
#+begin_src emacs-lisp
(use-package general
  :after evil
  :config
  (general-evil-setup)

  (general-create-definer custom/leader-keys
    :states '(normal visual insert emacs)
    :keymaps 'override
    :prefix "SPC"
    :global-prefix "M-m")

  ;; Normal mode
  (general-define-key
   :states 'normal
   "[d"  'flymake-goto-prev-error
   "]d"  'flymake-goto-next-error
   "gD"  'eglot-find-declaration
   "gI"  'eglot-find-implementation
   "C-p" 'project-find-file
   "C--" 'text-scale-decrease
   "C-=" 'text-scale-increase)

  (general-define-key
   :states '(normal visual)
   :keymaps 'override
   "K" 'custom/toggle-eldoc-tooltip)

  ;; Insert mode
  (general-define-key
   :states 'insert
   ;; Trigger completion with C-SPC
   "C-SPC" 'completion-at-point)

  ;; Dired
  (general-define-key
   :states 'normal
   :keymaps 'dired-mode-map
   "TAB" 'dired-toggle-read-only
   "o" 'dired-create-empty-file
   "O" 'dired-create-directory)

  ;; WDired
  (general-define-key
   :states 'normal
   :keymaps 'wdired-mode-map
   "=" 'wdired-finish-edit
   "ESC" 'wdired-abort-changes)

  (custom/leader-keys
   ;; Search
   "s"   '(:ignore t :which-key "search")
   "sg"  '(consult-ripgrep :which-key "grep")
   "sf"  '(project-find-file :which-key "project files")
   "sb"  '(consult-buffer :which-key "buffers")
   "ss"  '(consult-line :which-key "current file")
   "sr"  '(consult-recent-file :which-key "recent files")
   "st"  '(consult-todo-project :which-key "project todos")
   ;; Code
   "c"   '(:ignore t :which-key "code")
   "cf"  '(apheleia-format-buffer :which-key "format buffer")
   "ca"  '(eglot-code-actions :which-key "actions")
   "cr"  '(eglot-rename :which-key "rename")
   "cw"  '(whitespace-mode :which-key "toggle whitespace")
   "ch"  '(eldoc :which-key "hover documentation")
   ;; Git
   "g"   '(:ignore t :which-key "git")
   "gg"  '(magit-status :which-key "magit status")
   ;; Project
   "p"   '(:ignore t :which-key "project")
   "pp"  '(project-switch-project :which-key "switch project")
   "pb"  '(consult-project-buffer :which-key "project buffers")
   ;; Buffer
   "b"   '(:ignore t :which-key "buffer")
   "bf"  '(apheleia-format-buffer :which-key "format buffer")
   "bt"  '(consult-todo :which-key "search buffer todos")
   "bl"  '(consult-line :which-key "search buffer lines")
   ;; Other
   "."   '(find-file :which-key "navigate files")
   "RET" '(vterm-toggle :which-key "toggle vterm")
   "="   '(indent-region :which-key "indent region")))
#+end_src
